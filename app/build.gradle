plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.0'
	id 'io.spring.dependency-management' version '1.1.5'
	id 'maven-publish'
}

group = 'me.jamieburns'
version = '1.0-SNAPSHOT'

ext {
	myArtifactId = rootProject.name
}

bootJar {
	archiveBaseName = myArtifactId
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
        maven {
		name = 'GitHubPackagesQuicksort'
		url = uri('https://maven.pkg.github.com/jamie-burns0/quicksort')
		credentials {
			username = project.findProperty('gpr.user') ?: System.getenv('PACKAGES_USERNAME')
			password = project.findProperty('gpr.key') ?: System.getenv('PACKAGES_PERSONAL_ACCESS_TOKEN_CLASSIC')
		}
	}
	// mavenLocal()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	implementation 'me.jamieburns:quicksort:1.0-SNAPSHOT'
	implementation 'com.amazonaws.serverless:aws-serverless-java-container-springboot3:2.0.2'
}

tasks.named('test') {
	useJUnitPlatform()
}

publishing {
	publications {
		quicksortSpringboot(MavenPublication) {
			artifact bootJar
			artifactId =  myArtifactId
		}
	}
	repositories {
		//mavenLocal()
		maven {
			name = 'GitHubPackagesQuicksortSpringboot'
			url = uri('https://maven.pkg.github.com/jamie-burns0/quicksort-springboot')
			credentials {
				username = project.findProperty('gpr.user') ?: System.getenv('PACKAGES_USERNAME')
				password = project.findProperty('gpr.key') ?: System.getenv('PACKAGES_PERSONAL_ACCESS_TOKEN_CLASSIC')
			}
		}
	}
}

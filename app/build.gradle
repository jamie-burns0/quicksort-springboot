plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.5'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'maven-publish'
}

group = 'me.jamieburns'
version = '1.0-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
        maven {
		name = 'GitHubPackagesQuicksort'
		url = uri('https://maven.pkg.github.com/jamie-burns0/quicksort')
		credentials {
			username = project.findProperty('gpr.user') ?: System.getenv('PACKAGES_USERNAME')
			password = project.findProperty('gpr.key') ?: System.getenv('PACKAGES_PERSONAL_ACCESS_TOKEN_CLASSIC')
		}
	}
	// mavenLocal()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	implementation 'me.jamieburns:quicksort:1.0-SNAPSHOT'
}

tasks.named('test') {
	useJUnitPlatform()
}

publishing {
	publications {
		quicksortSpringboot(MavenPublication) {
			artifactId = 'quicksort-springboot'
			from components.java
		}
	}
	repositories {
		maven {
			name = 'GitHubPackagesQuicksortSpringboot'
			url = uri('https://maven.pkg.github.com/jamie-burns0/quicksort-springboot')
			credentials {
				username = project.findProperty('gpr.user') ?: System.getenv('PACKAGES_USERNAME')
				password = project.findProperty('gpr.key') ?: System.getenv('PACKAGES_PERSONAL_ACCESS_TOKEN_CLASSIC')
			}
		}
	}
}
